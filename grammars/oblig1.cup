package oblig1parser;
import java_cup.runtime.*;
import syntaxtree.*;
import java.util.*;

parser code {:

:};

/* Terminals */
terminal                        CLASS;
terminal                        LBRACK, RBRACK;
terminal                        LPAR, RPAR;
terminal                        SEMI, RETURN;
terminal                        PROCEDURE;

/********************/
terminal                        VAR;
terminal                        INT;
terminal						COMMA;
/*******************/
terminal String                 ID;
terminal String                 STRING_LITERAL;

/* Non terminals */
non terminal Program            program;
non terminal List<Decl>         decl_list;
non terminal Decl          class_decl, decl;

/*****************/
non terminal VarDecl            var_decl;
non terminal List<VarDecl>      var_decl_list, var_decl_list_bdy;
non terminal List<ParamDecl>    param_decl_list, param_decl_list_bdy;
non terminal ParamDecl   	    param_decl;
non terminal Type               type;
/*****************/


/* The grammar */
program                 ::= decl_list:dl param_decl_list:pdl
                        {: for (ParamDecl pd : pdl) { System.out.println(pd.printAst()); } RESULT = new Program(dl); :} 
                        ;

decl_list               ::= decl:d {: List<Decl> l = new LinkedList<Decl>();
                                      l.add(d);
                                      RESULT = l; :} 
                        |   decl_list:dl decl:d 
                            {: dl.add(d); RESULT = dl; :}
                        ;

decl                    ::= class_decl:sd {: RESULT = sd; :}
                        |   var_decl:vd {: RESULT = vd; :}
                        ;
                                  
class_decl              ::= CLASS ID:name LBRACK var_decl_list:vdl RBRACK
                        {: RESULT = new ClassDecl(name,vdl); :}
                        ;

/* 
 * var_decl_list -> [var_decl_list_bdy] 
 * var_decl_list -> EMPTY | var_decl_list_bdy
 */
var_decl_list		    ::= /* EMPTY */
						{: RESULT = new LinkedList<VarDecl>(); :}
						|	var_decl_list_bdy:vdl {: RESULT = vdl; :}	
						;

/*
 * var_decl_list_bdy -> {var_decl}
 * var_decl_list_bdy -> var_decl | var_decl_list_bdy var_decl
 */
var_decl_list_bdy       ::=	var_decl:vd 
						{: List<VarDecl> l = new LinkedList<VarDecl>();
                                         l.add(vd);
                                         RESULT = l; :}
                        | var_decl_list_bdy:vdl var_decl:vd {: vdl.add(vd); RESULT = vdl; :}
						;
						
var_decl                ::= VAR type:typename ID:name SEMI {:  RESULT = new VarDecl(name, typename); :}
                        ;
						
/* 
 * param_decl_list -> [param_decl_list_bdy] 
 * param_decl_list -> EMPTY | param_decl_list_bdy
 */
param_decl_list		    ::= /* EMPTY */
						{: RESULT = new LinkedList<ParamDecl>(); :}
						|	param_decl_list_bdy:pdl 
						{: RESULT = pdl; :}
						;

/*
 * param_decl_list_bdy -> param_decl {"," PARAM_DECL}
 * param_decl_list_bdy -> param_decl | param_decl_list_bdy "," param_decl 
 */
param_decl_list_bdy     ::=	param_decl:pd 
						{: List<ParamDecl> l = new LinkedList<ParamDecl>();
                                         l.add(pd);
                                         RESULT = l; :}
						| param_decl_list_bdy:pdl COMMA param_decl:pd 
						{: pdl.add(pd); RESULT = pdl; :}
						;
/*
 * param_decl -> ["var"] TYPE NAME
 * param_decl -> TYPE NAME | "var" TYPE NAME
 */ 
param_decl              ::= type:typename ID:name  
						{:  RESULT = new ParamDecl(name, typename, false); :}
						| VAR type:typename ID:name
						{:  RESULT = new ParamDecl(name, typename, true); :}
                        ;
						


type                    ::= INT {: RESULT = new Type("int"); :} 
                        ;
